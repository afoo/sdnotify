plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
    id 'org.cyclonedx.bom' version '1.7.4'
    id 'signing'
    id 'tech.yanand.maven-central-publish' version '1.3.0'
}

group = 'de.afoo'
version = '1.0-beta7'

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = "SDNotify"
                description = "Systemd notify client implementation for Java applications"
                url = "https://sdnotify.afoo.de"
                licenses {
                    license {
                        name = 'MIT No Attribution License'
                        url = 'https://opensource.org/licenses/MIT-0'
                    }
                }
                developers {
                    developer {
                        id = 'afoo'
                        name = 'Jan HÃ¼lsbergen'
                        email = 'jan@afoo.de'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/afoo/sdnotify.git'
                    url = 'https://github.com/afoo/sdnotify'
                }
            }
        }
    }
}


signing {
    useGpgCmd()
    sign publishing.publications.maven
}

mavenCentral {
    authToken = System.getenv('MAVEN_CENTRAL_TOKEN')
}

repositories {
    gradlePluginPortal()
    mavenCentral()
}

dependencies {
    // for UNIX DGRAM Sockets
    implementation 'com.kohlschutter.junixsocket:junixsocket-core:2.10.1'

    // Logging
    implementation 'org.slf4j:slf4j-api:2.0.16'

    // Jupiter
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.4'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.4'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // system-stubs
    testImplementation 'org.mockito:mockito-inline:5.2.0'
    testImplementation 'uk.org.webcompere:system-stubs-core:2.1.7'
    testImplementation 'uk.org.webcompere:system-stubs-jupiter:2.1.7'

    // UNIX DGRAM Server for tests
    testImplementation 'com.kohlschutter.junixsocket:junixsocket-server:2.10.1'

    // For FileUtils.touch
    testImplementation 'commons-io:commons-io:2.18.0'
}

test {
    useJUnitPlatform()
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacoco.exec")
    }
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
    }
}
